*** Settings *** 
Library          Selenium2Library
Test Setup		 Setup Browser
Test Teardown    Close All Browsers
Test Template    Collection Form Submit Should Fail When

Resource         ../Keywords/SearchKeywords.txt
Resource         ../Keywords/PersonalDataPageKeywords.txt
Resource         ../Keywords/ResultsPageKeywords.txt
Resource         ../Keywords/CollectionPageKeywords.txt


*** Variables ***
${COUNTRY}       ES
${BROWSER}       firefox
${HOMEPAGE}      http://francia:corcega@it.frontend.packitos.com/


*** Test Cases ***    NAME        SURNAME    EMAIL            PHONE        ADDRESS    
Name Is Empty    	  ${empty}    test       test@test.com    659659659    Italy
Surname Is Empty      test        ${empty}   test@test.com    659659659    Italy
Email Is Empty    	  test        test       ${empty}         659659659    Italy
Phone Is Empty    	  test        test       test@test.com    ${empty}     Italy
Address Is Empty   	  test        test       test@test.com    659659659    ${Empty}
Phone Is Too Short 	  test        test       test@test.com    6596         ${Empty}

*** Keywords ***
Collection Form Submit Should Fail When    [Arguments]    ${name}    ${surname}    ${email}    ${phone}    ${address}
	Select Date And Hour
	Fill Collection From Form With    ${name}    ${surname}    ${email}    ${phone}    ${address}
	Fill Collection Delivery Form With    ${name}    ${surname}    ${email}    ${phone}    ${address}
	Click Submit Button
	Submit Collection Form Unsuccessful
	
Setup Browser
	Open Browser    ${HOMEPAGE}    ${BROWSER}
	Maximize Browser Window
	Search Shipments With    1    10    10    10    25010    25010
	Select First Service
	Fill User Data Form With    Test    Test    test@test.com    test    5    True
	Click Next Step Button
	Must Be On Collection Data Page

	